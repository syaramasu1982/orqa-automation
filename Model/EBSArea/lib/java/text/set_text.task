@specification 1.1 
@param ( name = scenario ) 
@param ( name = session ) 
@param ( name = field ) 
@param ( name = title ) 
@param ( name = label ) 
@param ( name = alternateLabel ) 
@param ( name = index ) 
@param ( name = xPath ) 
@param ( name = value ) 
java.switch ( session = session ) 
@name ( 'Set text [${field}]' ) try {
	@name ( 'Populate value' ) if ^value != null && String.valueOf(value).trim().length() > 0 && label != null && String.valueOf(label).trim().length() > 0^ {
		@name ( 'Perform operation' ) control.retry ( maxAttempts = 5 ) {
			label = /EBSArea/lib/java/basic/check_label_exist.task ( scenario = scenario , session = session , name = label , alternateName = alternateLabel , index = index ) 
			java.waitforcomponent ( target = ^"//VTextField[@label='" + label + "']["+index+"]"^ , timeout = 5 ) 
			sleep 0.25 
			java.setvalue ( text = value , target = ^"//VTextField[@label='" + label + "']["+index+"]"^ ) 
			sleep 0.25 
			java.waitforcomponent ( target = ^"//VTextField[@label='" + label + "' and text()='" + value + "']"^ , timeout = 2 ) 
			sleep 0.25 
		} 
	} 
	@name ( 'Populate value' ) if ^value != null && String.valueOf(value).trim().length() > 0 && xPath != null && String.valueOf(xPath).trim().length() > 0^ {
		@name ( 'Perform operation' ) control.retry ( maxAttempts = 3 ) {
			java.waitforcomponent ( target = ^"//LWLabel[contains(text(),'"+ title +"')]//ancestor::ExtendedFrame//"+ xPath +""^ , timeout = 5 ) 
			sleep 0.25 
			java.setvalue ( text = value , target = ^"//LWLabel[contains(text(),'"+ title +"')]//ancestor::ExtendedFrame//"+ xPath +""^ ) 
		} 
	} 
} 
@name ( 'Handle failure' ) catch {
	/EBSArea/lib/java/basic/capture_screen_java.task ( scenario = scenario , session = session , name = ^ "set_text_"+ field +"_fail"^ ) 
	error ^"Failed to set text [" + field + "] on session [" + session + "]"^ 
} 
